plugins {
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.8.2'
}

apply plugin: 'eclipse'
apply plugin: 'java'
apply plugin: 'maven-publish'

wrapper.gradleVersion = '4.6'

version = 'development-SNAPSHOT'
group = 'com.github.edugonza'

ext {
	rapidprom_version = '4.0.2'
}

repositories {
	mavenLocal()
	mavenCentral()
	maven { url "https://jitpack.io" }
	ivy {
		url "https://github.com/rapidprom/rapidprom-libraries/raw/${rapidprom_version}/prom/"
		layout "pattern", {
			artifact "[module]-[revision]/[module]-[revision].[ext]"
			artifact "[module]-[revision]/[artifact].[ext]"
			ivy "[module]-[revision]/ivy-[module]-[revision].xml"
		}
	}
	ivy {
		url "https://github.com/rapidprom/rapidprom-libraries/raw/${rapidprom_version}/thirdparty/lib/"
		layout "pattern", {
			artifact "[module]-[revision]/[module]-[revision].[ext]"
			ivy "[module]-[revision]/ivy-[module]-[revision].xml"
		}
	}
	ivy {
		url "https://github.com/rapidprom/rapidprom-libraries/raw/${rapidprom_version}/thirdparty/resource/"
		layout "pattern", {
			artifact "[module]-[revision]/[module]-[revision].[ext]"
			ivy "[module]-[revision]/ivy-[module]-[revision].xml"
		}
	}
}

sourceSets {
	main {
		java {
			srcDirs = ['src/']
		}
		resources {
			srcDirs = ['resources/']
		}
	}
	test {
		java {
			srcDirs = ['test/']
		}
	}
}

jar {
	dependsOn javadoc
	from sourceSets.main.allSource
	from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        mavenLocal()
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

dependencies {
	compile  'log4j:log4j:1.2.16'
	compile  'org.jgrapht:jgrapht-core:1.0.0'
	compile  'org.xerial:sqlite-jdbc:3.21.0.1'
	compile  'org.rapidprom:XESLite:6.7.333'
	compile  'org.mapdb:mapdb:3.0.5'
	testCompile 'junit:junit:4.12'
}
